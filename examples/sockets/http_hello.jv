module http_hello
; Example program that listens on port 80 for HTTP requests, and responds with
; hello-world messages.

import io
import net
import process
import socket

; Creates the socket listening on port 80, wrapping it in HTTP.
sock = net.HttpSocket(
    socket.Socket(socket.AF_INET, socket.SOCK_STREAM, 0, '0.0.0.0', 80, 4, True))

io.println('Listening on port 80.')
io.println('End the program by pressing Enter.')

is_done = False
; It must be async; otherwise, it will block the rest of the program.
() async {
  io.IN.getline()
  is_done = True
  io.println('Detected user input. Will stop after next request.')
}()

handle_connection = (handle) async {
  request = handle.receive()
  handle.send(net.HttpResponse().add_content('Hello, world!'))
  io.println('Received request:')
  io.println(request)
  handle.close()
}

while ~is_done {
  handle_connection(sock.accept())
}

sock.close()
socket.cleanup()